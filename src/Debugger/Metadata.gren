module Debugger.Metadata exposing
    ( Error
    , Metadata
    , Problem(..)
    , ProblemType
    , check
    , decode
    , decoder
    , encode
    )

import Array exposing (Array)
import Debugger.Report as Report exposing (Report)
import Dict exposing (Dict)
import Json.Decode as Decode
import Json.Encode as Encode



-- METADATA


type alias Metadata =
    { versions : Versions
    , types : Types
    }



-- VERSIONS


type alias Versions =
    { elm : String
    }



-- TYPES


type alias Types =
    { message : String
    , aliases : Dict String Alias
    , unions : Dict String Union
    }


type alias Alias =
    { args : Array String
    , tipe : String
    }


type alias Union =
    { args : Array String
    , tags : Dict String (Array String)
    }



-- PORTABILITY


isPortable : Metadata -> Maybe Error
isPortable { types } =
    let
        badAliases =
            Dict.foldl collectBadAliases [] types.aliases
    in
    case Dict.foldl collectBadUnions badAliases types.unions of
        [] ->
            Nothing

        problems ->
            Just { message = types.message, problems = problems }


type alias Error =
    { message : String
    , problems : Array ProblemType
    }


type alias ProblemType =
    { name : String
    , problems : Array Problem
    }


type Problem
    = Function
    | Decoder
    | Task
    | Process
    | Socket
    | Request
    | Program
    | VirtualDom


collectBadAliases : String -> Alias -> Array ProblemType -> Array ProblemType
collectBadAliases name { tipe } list =
    case findProblems tipe of
        [] ->
            list

        problems ->
            Array.pushLast { name = name, problems = problems } list


collectBadUnions : String -> Union -> Array ProblemType -> Array ProblemType
collectBadUnions name { tags } list =
    case Array.flatMap findProblems (Array.flatten (Dict.values tags)) of
        [] ->
            list

        problems ->
            Array.pushFirst { name = name, problems = problems } list


findProblems : String -> Array Problem
findProblems tipe =
    Array.filterMap (hasProblem tipe) problemTable


hasProblem : String -> { problem : Problem, token : String } -> Maybe Problem
hasProblem tipe { problem, token } =
    if String.contains token tipe then
        Just problem

    else
        Nothing


problemTable : Array { problem : Problem, token : String }
problemTable =
    [ { problem = Function, token = "->" }
    , { problem = Decoder, token = "Json.Decode.Decoder" }
    , { problem = Task, token = "Task.Task" }
    , { problem = Process, token = "Process.Id" }
    , { problem = Socket, token = "WebSocket.LowLevel.WebSocket" }
    , { problem = Request, token = "Http.Request" }
    , { problem = Program, token = "Platform.Program" }
    , { problem = VirtualDom, token = "VirtualDom.Node" }
    , { problem = VirtualDom, token = "VirtualDom.Attribute" }
    ]



-- CHECK


check : Metadata -> Metadata -> Report
check old new =
    if old.versions.elm /= new.versions.elm then
        Report.VersionChanged old.versions.elm new.versions.elm

    else
        checkTypes old.types new.types


checkTypes : Types -> Types -> Report
checkTypes old new =
    if old.message /= new.message then
        Report.MessageChanged old.message new.message

    else
        []
            |> Dict.merge ignore checkAlias ignore old.aliases new.aliases
            |> Dict.merge ignore checkUnion ignore old.unions new.unions
            |> Report.SomethingChanged


ignore : String -> value -> a -> a
ignore key value report =
    report



-- CHECK ALIASES


checkAlias : String -> Alias -> Alias -> Array Report.Change -> Array Report.Change
checkAlias name old new changes =
    if old.tipe == new.tipe && old.args == new.args then
        changes

    else
        Array.pushFirst (Report.AliasChange name) changes



-- CHECK UNIONS


checkUnion : String -> Union -> Union -> Array Report.Change -> Array Report.Change
checkUnion name old new changes =
    let
        tagChanges =
            Dict.merge removeTag checkTag addTag old.tags new.tags <|
                Report.emptyTagChanges (old.args == new.args)
    in
    if Report.hasTagChanges tagChanges then
        changes

    else
        Array.pushFirst (Report.UnionChange name tagChanges) changes


removeTag : String -> a -> Report.TagChanges -> Report.TagChanges
removeTag tag _ changes =
    { changes | removed = Array.pushFirst tag changes.removed }


addTag : String -> a -> Report.TagChanges -> Report.TagChanges
addTag tag _ changes =
    { changes | added = Array.pushFirst tag changes.added }


checkTag : String -> a -> a -> Report.TagChanges -> Report.TagChanges
checkTag tag old new changes =
    if old == new then
        changes

    else
        { changes | changed = Array.pushFirst tag changes.changed }



-- JSON DECODE


decode : Encode.Value -> Result Error Metadata
decode value =
    case Decode.decodeValue decoder value of
        Err _ ->
            Err { message = "The compiler is generating bad metadata. This is a compiler bug!", problems = [] }

        Ok metadata ->
            case isPortable metadata of
                Nothing ->
                    Ok metadata

                Just error ->
                    Err error


decoder : Decode.Decoder Metadata
decoder =
    Decode.map2 (\versions types -> { versions = versions, types = types })
        (Decode.field "versions" decodeVersions)
        (Decode.field "types" decodeTypes)


decodeVersions : Decode.Decoder Versions
decodeVersions =
    Decode.map (\elm -> { elm = elm })
        (Decode.field "elm" Decode.string)


decodeTypes : Decode.Decoder Types
decodeTypes =
    Decode.map3
        (\message aliases unions ->
            { message = message
            , aliases = aliases
            , unions = unions
            }
        )
        (Decode.field "message" Decode.string)
        (Decode.field "aliases" (Decode.dict decodeAlias))
        (Decode.field "unions" (Decode.dict decodeUnion))


decodeUnion : Decode.Decoder Union
decodeUnion =
    Decode.map2 (\args tags -> { args = args, tags = tags })
        (Decode.field "args" (Decode.array Decode.string))
        (Decode.field "tags" (Decode.dict (Decode.array Decode.string)))


decodeAlias : Decode.Decoder Alias
decodeAlias =
    Decode.map2 (\args tipe -> { args = args, tipe = tipe })
        (Decode.field "args" (Decode.array Decode.string))
        (Decode.field "type" Decode.string)



-- JSON ENCODE


encode : Metadata -> Encode.Value
encode { versions, types } =
    Encode.object
        [ { key = "versions", value = encodeVersions versions }
        , { key = "types", value = encodeTypes types }
        ]


encodeVersions : Versions -> Encode.Value
encodeVersions { elm } =
    Encode.object [ { key = "elm", value = Encode.string elm } ]


encodeTypes : Types -> Encode.Value
encodeTypes { message, unions, aliases } =
    Encode.object
        [ { key = "message", value = Encode.string message }
        , { key = "aliases", value = encodeDict encodeAlias aliases }
        , { key = "unions", value = encodeDict encodeUnion unions }
        ]


encodeAlias : Alias -> Encode.Value
encodeAlias { args, tipe } =
    Encode.object
        [ { key = "args", value = Encode.array Encode.string args }
        , { key = "type", value = Encode.string tipe }
        ]


encodeUnion : Union -> Encode.Value
encodeUnion { args, tags } =
    Encode.object
        [ { key = "args", value = Encode.array Encode.string args }
        , { key = "tags", value = encodeDict (Encode.array Encode.string) tags }
        ]


encodeDict : (a -> Encode.Value) -> Dict String a -> Encode.Value
encodeDict f dict =
    dict
        |> Dict.map (\key value -> f value)
        |> Dict.toArray
        |> Encode.object
